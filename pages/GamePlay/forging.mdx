---
title: "Forging"
---

# Forging Guide

Forging involves the process of creating a new entity derived from the entropy of the two parent entities. Depending on your entity's role ("forger/merger"), players will have the in-game ability to forge their entities. The process of forging involves two separate entities. If an entity's ability to forge runs out (generation limit reached), then the next generation is minted with the genesis entropy off-by-one.

## Forging Process

1. **A forger lists itself with an ETH fee for the merger to pay.**
2. **A merger entity interacts and pays the forger's set ETH fee.**
3. **The two parent entities' entropies are added together and then divided by 2 to create the offspring entity with a new entropy.**

$$
\text{newEntropy} = \frac{\text{forgerEntropy} + \text{mergerEntropy}}{2}
$$

4. **The new entity is minted, inherits a new entropy based on the parents' entropies, and is transferred to the merger's owner address.**

The crucial parameters for an entity are “Nuke Factor”, “Forge Potential” and “Performance Factor”. These parameters are carried in an entity's entropy and are averaged upon forging. This will create a diverse forging marketplace with a mixture of high-low value entities for players to forge with.

**Important!** Remember to consider the forger's traits before forging with them. Since parent parameters are averaged, forging with a lower value entity will produce a less valuable entity than your current one.

## Code and Functions

### Function: mintNewEntity (forge process)

The `_mintNewEntity` function mints a new entity based on the parents' entropies. It checks if the current generation has reached its minting limit and, if so, increments to the next generation. The forging process will reject if the child's generation is already full. A new token ID is generated and the entity is minted to the sender's address. This function sets the entity's entropy, generation, and updates mint counts. It also records the initial owner. If the airdrop hasn't started, it adjusts the sender's airdrop amount based on the entropy. Finally, it emits an event indicating a new entity has been minted, specifying the sender, token ID, entropy, and current generation before returning the new token ID.

```solidity
function _mintNewEntity(
    uint256 entropy,
    uint256 gen
) private returns (uint256) {
    require(
        generationMintCounts[gen] < maxTokensPerGen,
        'Exceeds maxTokensPerGen'
    );

    uint256 newTokenId = _tokenIds++;
    _mint(msg.sender, newTokenId);

    tokenEntropy[newTokenId] = entropy;
    tokenGenerations[newTokenId] = gen;
    generationMintCounts[gen]++;
    initialOwners[newTokenId] = msg.sender;

    if (
        generationMintCounts[gen] >= maxTokensPerGen && gen == currentGeneration
    ) {
        _incrementGeneration();
    }

    if (!airdropContract.airdropStarted()) {
        airdropContract.addUserAmount(msg.sender, entropy);
    }

    emit NewEntityMinted(msg.sender, newTokenId, entropy, gen);
    return newTokenId;
} 
```

### Function Forge

The forging function in facilitates the forging between two entities, identified by parent1Id and parent2Id, to create a new entity. The function delegates the forging process to the entityForgingContract using the funds provided (msg.value). It then ensures that the call to forging is authorized by verifying that the caller is the entityForgingContract. The function calculates the new entities entropy by averaging the entropy values of the two parent entities. A new entity is minted with this entropy, and the contract increments the current generation, updating the total number of entity's in circulation. Finally, the function emits an event documenting the forging and returns the new entities ID.

```solidity
 function forge(
    uint256 parent1Id,
    uint256 parent2Id,
    string memory
  ) external whenNotPaused nonReentrant returns (uint256) {
    require(
      msg.sender == address(entityForgingContract),
      'unauthorized caller'
    );
    uint256 newGeneration = getTokenGeneration(parent1Id) + 1;

    /// Check new generation is not over maxGeneration
    require(newGeneration <= maxGeneration, "can't be over max generation");

    // Calculate the new entity's entropy
    (uint256 forgerEntropy, uint256 mergerEntropy) = getEntropiesForTokens(
      parent1Id,
      parent2Id
    );
    uint256 newEntropy = (forgerEntropy + mergerEntropy) / 2;

    // Mint the new entity
    uint256 newTokenId = _mintNewEntity(newEntropy, newGeneration);

    emit EntityForged(newTokenId, parent1Id, parent2Id, newEntropy);

    return newTokenId;
  }
```

### Function: listForForging

Allows the owner of a forger entity to list it for forging by setting a fee. This ensures the caller owns the entity and resets the forging count if needed. It retrieves the entity's entropy to determine its forging potential and checks if the entity has reached its forging limit. The function also verifies if the entity is a forger, based on its entropy. If all conditions are met, the entity is listed for forging with the specified fee, and an event is emitted to signal that the entity is available for forging.

```solidity
function listForForging(
    uint256 tokenId,
    uint256 fee
  ) public whenNotPaused nonReentrant {
    require(!listings[tokenId].isListed, 'Token is already listed for forging');
    require(
      nftContract.ownerOf(tokenId) == msg.sender,
      'Caller must own the token'
    );
    require(
      fee >= minimumListFee,
      'Fee should be higher than minimum listing fee'
    );

    _resetForgingCountIfNeeded(tokenId);

    uint256 entropy = nftContract.getTokenEntropy(tokenId); // Retrieve entropy for tokenId
    uint8 forgePotential = uint8((entropy / 10) % 10); // Extract the 5th digit from the entropy
    require(
      forgePotential > 0 && forgingCounts[tokenId] <= forgePotential,
      'Entity has reached its forging limit'
    );

    bool isForger = (entropy % 3) == 0; // Determine if the token is a forger based on entropy
    require(isForger, 'Only forgers can list for forging');

    listings[tokenId] = Listing(msg.sender, tokenId, true, fee);
    listedTokenIds[listingCount] = tokenId;
    listingCount++;

    emit ListedForForging(tokenId, fee);
  }
```